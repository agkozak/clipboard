#!/usr/bin/env zsh

if [[ -t 0 ]]; then
  # WSL 1 or 2
  if [[ $OSTYPE == linux* && -r /proc/version &&
      $(< /proc/version) == *[Mm]icrosoft* ]]; then
    powershell.exe -NoProfile -NonInteractive -Command Get-Clipboard
  elif (( $+commands[xclip] )); then
    xclip -selection clipboard -out
  elif (( $+commands[xsel] )); then
    xsel -b
  elif [[ $OSTYPE == darwin* ]]; then
    command pbpaste
  elif [[ $OSTYPE == (cygwin*|msys) ]]; then
    cat /dev/clipboard
  fi
else
  if [[ $OSTYPE == linux* && -r /proc/version &&
      $(< /proc/version) == *[Mm]icrosoft* ]]; then
    # WSL
    clip.exe
  elif (( $+commands[xclip] )); then
    xclip -selection clipboard -in
  elif (( $+commands[xsel] )); then
    xsel -b
  elif [[ $OSTYPE == darwin* ]]; then
    command pbcopy
  elif [[ $OSTYPE == (cygwin*|msys) ]]; then
    tee > /dev/clipboard
  fi
fi
